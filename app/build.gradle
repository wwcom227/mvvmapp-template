import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}
apply from: 'template-build/scripts/proguards.gradle'

android {
    compileSdk 31

    defaultConfig {
        applicationId App.appId
        minSdkVersion App.minSdkVersion
        targetSdkVersion App.targetSdkVersion
        versionCode App.versionCode
        versionName App.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        buildConfigField "boolean", "IS_SHOW_LOG", "false"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        resConfigs "en", "zh-rCN"
    }

    buildTypes {
        // debug
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            debuggable true
            matchingFallbacks = ['debug']
        }
        // release
        release {
            minifyEnabled true
            debuggable false
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getAllProguardFiles()
            matchingFallbacks = ['release']
        }
    }

    signingConfigs {
        debug {
            storeFile file("template-build/template.keystore")
            storePassword "template123"
            keyAlias "template"
            keyPassword "template123"
            v2SigningEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def buildDate = new SimpleDateFormat("yyyyMMddHHmm").format(new Date())
                def fileName = "${App.appName}_${buildDate}.apk"
                if (variant.buildType.name == 'release') {
                    fileName = "${App.appName}_${defaultConfig.versionName}_${buildDate}.apk"
                } else if (variant.buildType.name == 'debug') {
                    fileName = "${App.appName}_${defaultConfig.versionName}_debug_${buildDate}.apk"
                }
                outputFileName = fileName
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    //AndroidX相关
    implementation Deps.kotlinStdlib
    implementation Deps.androidx_material
    implementation Deps.androidx_appcompat
    implementation Deps.androidx_recyclerView
    implementation Deps.androidx_constraintLayout
    implementation Deps.androidx_legacy
    implementation Deps.androidx_multidex

    // third part
    implementation Deps.spinner
    implementation Deps.fresco
    implementation Deps.base_adapter
    implementation Deps.event_bus
    implementation Deps.swipe
    implementation Deps.animation
    implementation Deps.fancybuttons

    implementation project(path: ':base')
}